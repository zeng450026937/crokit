
declare_args() {
  node_host_binary = "node"
  node_build = true
}

node_addon_configs = [
  "//yealink/node:node_lib_header",
  "//yealink/node:node_lib_config",
]

node_remove_configs = []

if (is_clang) {
  node_remove_configs += [
    "//build/config/clang:find_bad_constructs",
    "//build/config/compiler:no_exceptions",
    "//build/config/compiler:chromium_code",
  ]
  node_addon_configs += [
    "//build/config/compiler:no_chromium_code",
  ]
}

set_defaults("node_addon") {
  win_delay_load_hook = true
  configs = node_addon_configs
  suppressed_configs = []
}

template("node_addon") {
  loadable_module(target_name) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "configs",
                             "public_configs",
                             "suppressed_configs",
                           ])

    configs += invoker.configs
    configs -= node_remove_configs
    configs -= invoker.suppressed_configs

    public_configs = []
    if (defined(invoker.public_configs)) {
      public_configs += invoker.public_configs
    }

    ldflags = []
    if (defined(invoker.ldflags)) {
      ldflags += invoker.ldflags
    }

    output_extension = "node"

    defines = [
      "NODE_GN_MODULE_NAME=" + target_name,
      "NODE_GYP_MODULE_NAME=" + target_name,
      "BUILDING_NODE_EXTENSION"
    ]

    if (node_build) {
      defines += [ "NODE_BUILD" ]
    }

    if (is_win && invoker.win_delay_load_hook) {
      defines += [ "HOST_BINARY=\"${node_host_binary}.exe\"" ]
      sources += [
        "//yealink/node/win_delay_load_hook.cc"
      ]
      ldflags += [
        "/DELAYLOAD:${node_host_binary}.exe",
      ]
    }
  }
}
